# Atoms
const {ok: void} ();
const {nil: qexpr} {};
const {true: int} 1;
const {false: int} 0;

# Unpack and Pack List for function
const {unpack: func} {f: func q: qexpr} -> {
	eval (join (list f) q)
};
const {pack: func} {f: func &xs: int} -> {f xs};
const {curry: func} unpack;
const {uncurry: func} pack;

const {do: func} {&q: any} -> {
	if (== q nil)
		{nil}
		{last q}
};

# Open a new scope
const {scope: func} {b: qexpr} -> {
	{_: any} -> b () # TODO: '{} -> b' should also work (although, it doesn't right now)
};

const {flip: func} {f: func a: any b: any} -> {f b a};

const {map: func} {f: func q: qexpr} -> {
	if (== q nil)
		{nil}
		{join (list (f (get 0 q))) (map f (tail q))}
};

const {isok: func} {a: any} -> {
	if (== a ok)
		{true} {false}
};

const {isnok: func} {a: any} -> {
	if (!= a ok)
		{true} {false}
};

const {iserr: func} {a: any} -> {
	if (== (typeof a) err)
		{true} {false}
};

const {iferrdo: func} {a: any f: func} -> {
	if (== (typeof a) err)
		{f a} {ok}
};

const {ghost: func} {&xs: any} -> {eval xs};

# Compose Functions Together
const {comp: func} {f: func g: func x: any} -> {f (g x)};

const {first_eval: func} (comp eval first);

const {filter: func} {f: func q: qexpr} -> {
	if (== q nil)
		{nil}
		{join (if (f (first_eval q)) {first q} {nil}) (filter f (tail q))}
};

def {foldl: func} {f: func z: any q: qexpr} -> {
	if (== q nil)
		{z}
		{foldl f (f z (first_eval q)) (tail q)}
};

def {sum: func} {q: qexpr} -> {foldl + 0 q};
def {product: func} {q: qexpr} -> {foldl * 1 q};

def {fib: func} {n: int} -> {
	if (== n 0) {0}
		(== n 1) {1}
		{+ (fib (- n 1)) (fib (- n 2))}
};